#include "c.ceu"
#include "sdl/sdl.ceu"

native/const _SDLK_SPACE;

var int height = 480;
var int width  = 640;

var&? SDL_Init sdl =
    spawn SDL_Init("Asteroid", width, height, SDL_Color(0xFF,0xFF,0xFF,0xFF));

watching sdl do

    code/await Cloud (var SDL_Rect r, var float vx) -> FOREVER do
        var float x = -r.w;
        par do
            var int dt;
            every dt in SDL_DT do
                x = x + vx*dt/1000;
                if x > 640 then
                    x = -r.w;
                end
            end
        with
            every SDL_REDRAW do
                _SDL_SetRenderDrawColor(&&outer.sdl!.ren, 0xDD,0xDD,0xDD,0xFF);
                r.x = x as int;
                _SDL_RenderFillRect(&&outer.sdl!.ren, (&&r as _SDL_Rect&&));
            end
        end
    end
	       
	code/await Bullet (var float playerX, var float playerY) -> FOREVER do
		var float x = playerX - 5/2;
		var float y = playerY;
        var float vy = -200;

        var SDL_Rect r = val SDL_Rect(0,0, 5, 20);

		par do
            var int dt;
            every dt in SDL_DT do
                y = y + vy*dt/1000;
            end
        with
            every SDL_REDRAW do
                _SDL_SetRenderDrawColor(&&outer.sdl!.ren, 255,0,0,0xFF);
                r.x = x as int;
                r.y = y as int;
                _SDL_RenderFillRect(&&outer.sdl!.ren, (&&r as _SDL_Rect&&));
            end
        end
	end	
	
    code/await Player (var int screenWidth, var int screenHeight) -> FOREVER do
		
		var float x = screenWidth/2 - 50/2;
        var float y = screenHeight - 20/2;
        var float vx = 0;
        var float vy = 0;

        var SDL_Rect r = val SDL_Rect(0,0, 50,20);

        par do
            var int dt;
            every dt in SDL_DT do
                x = x + vx*dt/1000;
                y = y + vy*dt/1000;
            end
        with
            par do
                loop do
                    var _SDL_KeyboardEvent&& key;
                    key = await SDL_KEYDOWN until key:keysym.sym==_SDLK_LEFT;
                    vx = -100;
                    key = await SDL_KEYUP   until key:keysym.sym==_SDLK_LEFT;
                    vx = 0;
                end
            with
                loop do
                    var _SDL_KeyboardEvent&& key;
                    key = await SDL_KEYDOWN until key:keysym.sym==_SDLK_RIGHT;
                    vx = 100;
                    key = await SDL_KEYUP   until key:keysym.sym==_SDLK_RIGHT;
                    vx = 0;
                end
			with
				loop do
					pool[] Bullet bullets;
					var _SDL_KeyboardEvent&& key;
                    key = await SDL_KEYDOWN until key:keysym.sym==_SDLK_SPACE;
                    spawn Bullet(r.x + 50/2, r.y) in bullets;
                    key = await SDL_KEYUP   until key:keysym.sym==_SDLK_SPACE;
				end
            end
        with
            every SDL_REDRAW do
                _SDL_SetRenderDrawColor(&&outer.sdl!.ren, 0x00,0x00,0x00,0xFF);
                r.x = x as int;
                r.y = y as int;
                _SDL_RenderFillRect(&&outer.sdl!.ren, (&&r as _SDL_Rect&&));
            end
        end
    end

    native/pre do
        ##include <time.h>
    end
    _srand(_time(null));

    pool[3] Cloud clouds;
    var int i;
    loop i in [1 -> 3] do
        var int w = 50 + _rand()%200;
        var int h = 20 + _rand()%50;
        var int y = h + _rand()%200;
        spawn Cloud(SDL_Rect(0,y,w,h), 10+_rand()%50) in clouds;
    end

    spawn Player(width, height);

    await FOREVER;
end

escape 0;
